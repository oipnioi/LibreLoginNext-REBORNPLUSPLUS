plugins {
    id 'org.cadixdev.licenser' version '0.6.1'
}

defaultTasks 'updateLicenses', 'shadowJar'

version '0.25.1'

subprojects {
    version = rootProject.version
    group = 'com.github.MiguVerse.LibreLoginNext'

    apply plugin: 'org.cadixdev.licenser'
    apply plugin: 'maven-publish'

    repositories {
        mavenLocal()
        mavenCentral()

        maven {
            url 'https://jitpack.io'
        }
    }

    license {
        header = rootProject.file('HEADER.txt')
        include '**/*.java'
        newLine = true

        matching("**/protocollib/EncryptionUtil.java") {
            header = rootProject.file("licenses/FASTLOGIN_LICENSE")
        }
    }

    // JitPack publishing configuration for multi-module projects
    afterEvaluate {
        publishing {
            publications {
                maven(MavenPublication) {
                    groupId = project.group
                    artifactId = project.name
                    version = project.version
                    
                    from components.java
                }
            }
        }
    }
}
